#version 460 core

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in; // invocations

layout(binding = 0, r32ui) uniform uimage2D out_density;
layout(binding = 1, r32ui) uniform uimage2D out_colour;

uniform float u_time_elapsed;

ivec2 ClampCoord(ivec2 coord) {
    return clamp(coord, ivec2(0), ivec2(imageSize(out_density)));
}


vec2 AffineTransform(vec2 coord) {
    float l2 = dot(coord, coord);
    return vec2(coord.x * sin(l2) - coord.y * cos(l2), coord.x * cos(l2) + coord.y * sin(l2));
}

#define PI 3.14
vec2 Popcorn(vec2 coord, float c, float d) {
    return vec2(c*coord.x + sin(tan(3*coord.y)), d*coord.y + sin(tan(3*coord.x)));
}

vec2 Horseshoe(vec2 c) {
    return vec2((c.x - c.y) * (c.x + c.y), 2 * c.x * c.y) / length(c);
}

vec2 Sinusoidal(vec2 c) {
    return sin(c);
}

vec2 RotateTransform(vec2 coord, float theta) {
   theta = (theta + 90.0) * PI / 180.0;
    mat2 rotation = mat2(
        cos(theta), -sin(theta),
        sin(theta), cos(theta)
    );

    return rotation * coord;
}

float rand(vec2 co){
    return fract(sin(dot(co, vec2(12.9898, 78.233))) * 43758.5453);
}

uint PackVec3(vec3 c) {
    uint r = uint(min(c.r * 255.0, 255.0));
    uint g = uint(min(c.g * 255.0, 255.0));
    uint b = uint(min(c.b * 255.0, 255.0));
    uint col_bits = 0;
    col_bits = col_bits | (r << 24);
    col_bits = col_bits | (g << 16);
    col_bits = col_bits | (b << 8);

    return col_bits;
}

vec3 UnpackVec3(uint val) {
    uint r = (val >> 24);
    uint g = ((val >> 16) & 0x000000FF);
    uint b = ((val >> 8) & 0x000000FF);

    return vec3(r / 255.0, g / 255.0, b / 255.0);
}

vec2 f0(vec2 coord) {
    coord = RotateTransform(coord, PI * 0.25);
    return coord;
}

vec2 f1(vec2 coord) {

    for (int i = 1; i < 5; i++) {
        float e = float(i);
        for (int y = 1; y < 5; y++) {
         coord = Horseshoe(coord / (i*10.25 / y));
         coord *= 0.75;
        }
    }

    return coord;
}

vec2 f2(vec2 coord) {
    coord = Horseshoe(coord*10*tan(coord.x));
    coord = Popcorn(coord*10.1*tan(coord.y), 0.5, 1.0);
    coord = Popcorn(coord*0.0001, 10.0, 1.5);

    return coord;
}

vec2 f3(vec2 coord) {
    coord = RotateTransform(coord, PI);
    coord = sin(coord*6);

    return coord;
}

vec2 FinalTransform(vec2 coord) {
    coord = RotateTransform(coord, PI*0.5) / pow(dot(coord, coord), 0.57);
    return coord.xy * 0.45;
}

vec2 Ifs(vec2 coord) {
    const float probabilities[] = {0.4, 0.3, 0.15, 0.25};
    float cdf[4];

    {
        float sum = 0.f;
        for (int i = 0; i < 4; i++) {
            sum += probabilities[i];
            cdf[i] = sum;
        }
    }

    // Converge a bit first
    for (uint i = 0; i < 20; i++) {
        float r = rand(vec2(coord.x, coord.y + i));

        if (r < cdf[0]) {
            coord = f0(coord);
        }
        else if (r < cdf[1]) {
            coord = f1(coord);
        }
        else if (r < cdf[2]) {
            coord = f2(coord);
        } else if (r < cdf[3]) {
            coord = f3(coord);
        }

        coord = FinalTransform(coord);
    } 

    for (uint i = 0; i < 1000; i++) {
        float r = rand(vec2(coord.x, coord.y + i + 50));
        vec3 col;

        if (r < cdf[0]) {
            coord = f0(coord);
            col = vec3(0.35, 0.15, 0.0);
        }
        else if (r < cdf[1]) {
            coord = f1(coord);
            col = vec3(0, 0.8, 0.3);
        }
        else if (r < cdf[2]) {
            coord = f2(coord);
            col = vec3(0.15, 0.3, 0.8);
        } else if (r < cdf[3]) {
            coord = f3(coord);
            col = vec3(0.0, 0.2, 0.6);
        }

        coord = FinalTransform(coord);
        ivec2 write_coord = ivec2((coord + 1.0) * 0.5 * vec2(imageSize(out_colour)));

        if (any(lessThan(write_coord, vec2(0))) || any(greaterThan(write_coord, imageSize(out_colour)))) continue;

        uint val = imageAtomicOr(out_colour, ClampCoord(ivec2((coord + 1.0) * 0.5 * vec2(imageSize(out_colour)))), 0u);
        vec3 pixel_col = UnpackVec3(val);
        pixel_col = (pixel_col + col) * 0.5;
        val = PackVec3(pixel_col);
        imageAtomicOr(out_colour, ClampCoord(ivec2((coord + 1.0) * 0.5 * vec2(imageSize(out_colour)))), val);
        imageAtomicAdd(out_density, ClampCoord(ivec2((coord + 1.0) * 0.5 * vec2(imageSize(out_colour)))), 1);
    }

    return coord;
}

void main() {
#ifdef CLEAR
    imageStore(out_density, ivec2(gl_GlobalInvocationID.xy), uvec4(0));
    imageStore(out_colour, ivec2(gl_GlobalInvocationID.xy), uvec4(0));
#else
    vec2 coord = gl_GlobalInvocationID.xy / vec2(imageSize(out_density));

    // TODO: fix aspect ratio
    coord = 2.0 * (coord - 0.5);
    Ifs(coord);
#endif
}